apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

apply plugin: 'com.jfrog.bintray'

if (JavaVersion.current().isJava8Compatible()) {
  allprojects {
    tasks.withType(Javadoc) {
      options.addStringOption('Xdoclint:none', '-quiet')
    }
  }
}

def pomConfig = {
  licenses {
    license {
      name "The Apache Software License, Version 2.0"
      url "http://www.apache.org/licenses/LICENSE-2.0.txt"
      distribution "repo"
    }
  }
  developers {
    developer {
      id "$project.user"
      name "$project.fullname"
      email "$project.email"
    }
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId "$project.group"
      artifactId "$project.artifact"
      //noinspection GroovyAssignabilityCheck
      version "$project.version"

      from components.java
      artifact sourcesJar
      artifact javadocJar
      pom.withXml {
        def root = asNode()
        root.appendNode('description', "$project.description")
        root.children().last() + pomConfig
      }
    }
  }
}

bintray {
  user = project.hasProperty('bintrayUser') ? project.bintrayUser : System.getenv('BINTRAY_USERNAME')
  key = project.hasProperty('bintrayKey') ? project.bintrayKey : System.getenv('BINTRAY_KEY')
  publications = ['mavenJava']
  pkg {
    repo = 'maven'
    name = "$project.codename"
    desc = "$project.description"
    websiteUrl = "$project.website"
    issueTrackerUrl = "$project.issues"
    licenses = ['Apache-2.0']
    vcsUrl = "$project.website"
    //noinspection GroovyAssignabilityCheck
    version {
      name = "$project.version"
      desc = "$project.description"
      released = new Date()
      vcsTag = "$project.website"
    }
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier 'javadoc'
  //noinspection GrUnresolvedAccess
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

task installToLocalRepo(dependsOn: ['publishMavenJavaPublicationToMavenLocal']) {
  logger.info "Installing $project.name"
}